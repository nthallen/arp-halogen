%{
  #include <math.h>
  #include "dtoe.h"
%}
TM typedef double mixing_ratio { text "%7.2lf"; }
mixing_ratio ClO_A; Invalidate ClO_A;

depending on (DetA_CD once, DetA_Z once, SD1_P_Z once, SD2_P_Z once, SF1FTemp_Z once) {
  double Qa, bkA, Sfa, NO_conv_eff_A;
  double TA, P, MA, Lyman_A, dsdmA;
  Qa = 0.0184/3.28e16;
  bkA = DetA_Z;
  Sfa = DetA_CD - DetA_Z;
  NO_conv_eff_A = 0.75;
  TA = SF1FTemp_Z + 273;
  P = (SD1_P_Z + SD2_P_Z)/2;
  MA = 9.66e18*P/TA;
  Lyman_A = 0.95;
  dsdmA = bkA * exp(7.5*2.0e-20*0.21*MA)/(1/Qa + 0.857 * MA);
  ClO_A = Sfa                    /* Corrected Fluor signal */
     *(2.67e-29*MA + 3.65e-11)   /* M dependent cal in air */
     *(1.0/dsdmA)               /* 1/dsdm in N2 */
     *(exp(7.5*2.0e-20*0.21*MA)) /* correction for Abs by O2 */
     *sqrt((500 + 2*TA)/1100)   /* Temp correction */
     *(1.0/NO_conv_eff_A)       /* NO conversion efficiency */
     *(1.0/Lyman_A)             /* Lyman-a correction */
     *(1.0e12/MA);              /* conversion to ppt */
  Validate ClO_A;
}

double dsdmB;
mixing_ratio ClONO2_B; Invalidate ClONO2_B;

depending on (DetB_Z once, DetB_AB once, SF1BTemp_Z once, SD1_P_Z once,
              SD2_P_Z once, SF1B5_Z once) {
  double Qb, bkB, Sfb, NO_conv_eff_B;
  double P, TB, M_B, Lyman_B, Tx;
  Tx = 210.;
  Qb = 0.0126/3.28e16;
  bkB = DetB_Z;         /* x4 not implemented here */
  Sfb = DetB_AB-DetB_Z;
  NO_conv_eff_B = 0.9;
  P = (SD1_P_Z + SD2_P_Z)/2;
  TB = SF1BTemp_Z + 273;
  M_B = 9.66e18*P/TB;
  Lyman_B = 0.96;
  if (SF1BTemp_Z < 80) {
    dsdmB = bkB*exp(7.5*2.0e-20*0.21*M_B) / (1/Qb + 0.857*M_B);
  }
  ClONO2_B = SF1B5_Z >= Tx ? Sfb   /* Corrected Fluor signal */
     *(2.67e-29*M_B + 3.65e-11)    /* M dependent cal in air */
     *(1.0/dsdmB)                  /* 1/dsdm in N2 */
     *(exp(7.5*2.0e-20*0.21*M_B))  /* correction for Abs by O2 */
     *sqrt((500 + 2*TB)/1100)      /* Temp correction */
     *(1.0/NO_conv_eff_B)          /* NO conversion efficiency */
     *(1.0/Lyman_B)                /* Lyman-a correction */
     *(1.0e12/M_B) : -99999.;      /* conversion to ppt */
  /* ClONO2 mixing ratio in ppt with NaNs where T < T_threshold */
  Validate ClONO2_B;
}
